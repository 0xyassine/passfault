apply plugin: 'maven'
apply plugin: 'signing'
defaultTasks 'clean', 'test'

sourceCompatibility = 1.6

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4+'
}

jar {
	manifest {
		attributes 'Implementation-Title': 'OWASP Passfault', 
			'Implementation-Version': version,
			'Main-Class': 'org.owasp.passfault.TextAnalysis'
	}	
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'passfault-core'
               packaging 'jar'
               description 'OWASP Passfault evaluates password strength and enforces password policy'
               url 'https://passfault.com/'

               scm {
		url 'scm:git@github.com:c-a-m/passfault'
                connection 'scm:git@github.com:c-a-m/passfault/GradleFx.git'
                developerConnection 'scm:git@github.com:c-a-m/passfault/GradleFx.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'c-a-m'
                       name 'Cam Morris'
                   }
               }
           }
        }
    }
}
